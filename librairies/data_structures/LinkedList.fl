using "List.fl";


LinkedList : this |-> {
    List(this);
    this :~ LinkedList;

    this->_first := ();
    this->_last := ();
    this->_lenght := 0;

    this : index |-> {
        i := 0;
        cell := this->first;
        while (i < index) {
            cell := cell->next;
            ++i
        };
        cell->object
    }

    this->foreach : function |-> {
        cell := this->_first;
        while (cell != ()) {
            function(cell->object);
            cell := cell->next
        };
        ()
    };

    this->lenght : () |-> {
        this->_lenght
    }

    this->is_empty : () |-> {
        this->_first = ()
    };

    this->get_first : () \ (this->_first != ()) |-> {
        this->_first->object
    };

    this->get_last : () \ (this->_last != ()) |-> {
        this->_last->object
    };

    this->add_first : a |-> {
        tmp->object := a;
        tmp->previous := ();
        tmp->next := this->_first;
        if (this->_first != ()) {
            this->_first->previous := tmp
        } else {
            this->_last := tmp
        };
        this->_first := tmp;
        ++this->_lenght;
        tmp->object
    };

    this->add_last : a |-> {
        tmp->object := a;
        tmp->previous := this->_last;
        tmp->next := ();
        if (this->_last != ()) {
            this->_last->next := tmp
        } else {
            this->_first := tmp
        };
        this->_last := tmp;
        ++this->_lenght;
        tmp->object
    };

    this->remove_first : () \ (this->_first != ()) |-> {
        tmp := this->_first->object;
        this->_first := this->_first->next;
        if (this->_first != ()) {
            this->_first->previous := ()
        } else {
            this->_last := ()
        };
        --this->_lenght;
        tmp
    };

    this->remove_last : () \ (this->_last != ()) |-> {
        tmp := this->_last->object;
        this->_last := this->_last->previous;
        if (this->_last != ()) {
            this->_last->next := ()
        } else {
            this->_first := ()
        };
        --this->_lenght;
        tmp
    };

    this->insert : (index, object) \ (0 <= index & index < this->_lenght) |-> {
        i := 0;
        cell := this->_first;
        while (i < index) {
            cell := cell->next;
            ++i
        };
        
        tmp->object := object;
        tmp->previous := cell->previous;
        tmp->next := cell;

        if (cell->previous != ()) {
            cell->previous->next := tmp
        } else {
            this->iterable->_first := tmp
        };
        cell->previous := tmp;

        tmp->object;
    };

    this->remove : index \ (0 <= index & index < this->_lenght) |-> {
        i := 0;
        cell := this->_first;
        while (i < index) {
            cell := cell->next;
            ++i
        };

        cell->previous->next := cell->next;
        cell->next->previous := cell->previous;
        cell->object
    };

    this->iterator_first : () \ (this->_first != ()) |-> {
        cell->object := ();
        cell->previous := ();
        cell->next := this->_first;
        LinkedList->Iterator((), this, cell)
    };

    this->iterator_last : () \ (this->_last != ()) |-> {
        cell->object := ();
        cell->previous := this->_last;
        cell->next := ();
        LinkedList->Iterator((), this, cell)
    };

    this->iterator : this->iterator_first;

    this->iterator : index \ (0 <= index & index < this->_lenght) |-> {
        i := 0;
        cell := this->first;
        while (i < index) {
            cell := cell->next;
            ++i
        };
        LinkedList->Iterator((), this, cell)
    };

    this
};

LinkedList->Iterator : (this, list, cell) |-> {
    List->Iterator(this, list);
    this :~ LinkedList->Iterator;

    this->_cell := cell;

    this->has_previous : () |-> {
        this->_cell->previous != ()
    };

    this->previous : () \ (this->has_previous()) |-> {
        this->_cell := this->_cell->previous;
        this->_cell->object
    };

    this->has_next : () |-> {
        this->_cell->next != ()
    };

    this->next : () \ (this->has_next()) |-> {
        this->_cell := this->_cell->next;
        this->_cell->object
    };

    this->get : () |-> {
        this->_cell->object
    };

    this->remove : () |-> {
        if (this->_cell->previous != ()) {
            this->_cell->previous->next := cell->next
        } else {
            this->iterable->first := cell->next
        };

        if (this->_cell->next != ()) {
            this->_cell->next->previous := cell->previous
        } else {
            this->iterable->last := cell->previous
        };
        
        this->_cell->object
    };

    this->insert : object |-> {
        tmp->object := object;
        tmp->previous := this->_cell->previous;
        tmp->next := this->_cell;

        if (this->_cell->previous != ()) {
            this->_cell->previous->next := tmp
        } else {
            this->iterable->_first := tmp
        };
        this->_cell->previous := tmp;

        tmp->object
    };

    this
};

(@) : (element, list) \ (list ~ LinkedList) |-> {
    found := false;
    it := list->iterator();
    while (!found & it->has_next()) {
        if (it->next() = element) {
            found := true
        }
    };
    found
};

(@==) : (element, list) \ (list ~ LinkedList) |-> {
    found := false;
    it := list->iterator();
    while (!found & it->has_next()) {
        if (it->next() === element) {
            found := true
        }
    };
    found
}